plugins {
    id 'org.springframework.boot' version '2.1.5.RELEASE'
    id 'java'
    // Provide convenience executables for trying out the examples.
    id 'application'
    // ASSUMES GRADLE 2.12 OR HIGHER. Use plugin version 0.7.5 with earlier gradle versions
    id 'com.google.protobuf' version '0.8.8'
    // Generate IntelliJ IDEA's .idea & .iml project files
    id 'idea'
}

apply plugin: 'io.spring.dependency-management'

group = 'me.zaccoding'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

def grpcVersion = '1.21.0'
def protocVersion = '3.7.1'

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

idea {
    module {
        sourceDirs += file("${projectDir}/src/generated/main/java");
        sourceDirs += file("${projectDir}/src/generated/main/grpc");
    }
}

startScripts.enabled = false

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/repos/central/data/"
    }

    mavenLocal()
}

ext {
    set('springCloudVersion', "Greenwich.SR1")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-zuul'
    compile 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // web3j
    implementation 'org.web3j:core:4.5.6'

    // metrics
    implementation group: 'io.dropwizard.metrics', name: 'metrics-core', version: '4.1.0'
    implementation group: 'io.dropwizard.metrics', name: 'metrics-healthchecks', version: '4.1.0'

    // common io
    implementation group: 'commons-io', name: 'commons-io', version: '2.6'

    // zookeeper
    implementation group: 'org.apache.zookeeper', name: 'zookeeper', version: '3.5.5'

    // grpc
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-netty:${grpcVersion}"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}
